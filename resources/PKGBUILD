# Maintainer: Callum Oz <acheronfail@gmail.com>
pkgname=i3-plasma-integration
pkgver=0.2.0
pkgrel=1
# TODO depend on version >= 4.18
depends=('i3-wm')
makedepends=('cargo')
arch=('i686' 'x86_64')
pkgdesc="An IPC addon for i3 to integrate it better with plasma"
url="https://github.com/acheronfail/$pkgname"
license=('MIT' 'Apache' 'custom')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8989eda337aa1c2c1d1f49e653c43a6a44d9dca266582c6b11d174455a1cd5deb15430afd54203d48a76374686c5dd2603dbede425a829c04c02bf61042c8fbf')

build() {
  cd "$pkgname-$pkgver"

  cargo build --release --locked --all-features
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -Dm644 "target/release/build/$pkgname-"*/out/_"$pkgname" "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 "target/release/build/$pkgname-"*/out/"$pkgname".bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "target/release/build/$pkgname-"*/out/"$pkgname".fish "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"

  install -Dm644 "doc/${pkgname}.1" "$pkgdir/usr/share/man/man1/${pkgname}.1"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE-MIT"
  install -Dm644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/${pkgname}/APACHE"
  install -Dm644 "LICENSE-UNLICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE-UNLICENSE"
}
